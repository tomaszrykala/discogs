apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.tomaszrykala.discogs"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'com.tomaszrykala.discogs.MockDiscogsAppRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

// credit: http://www.rainbowbreeze.it/environmental-variables-api-key-and-secret-buildconfig-and-android-studio/
def DISCOGS_CONSUMER_KEY = '"' + DiscogsConsumerKey + '"' ?: '"Define Discogs Consumer key"';
def DISCOGS_CONSUMER_SECRET = '"' + DiscogsConsumerSecret + '"' ?: '"Define Discogs Consumer secret"';

android.buildTypes.each {
    type ->
        type.buildConfigField 'String', 'DISCOGS_CONSUMER_KEY', DISCOGS_CONSUMER_KEY
        type.buildConfigField 'String', 'DISCOGS_CONSUMER_SECRET', DISCOGS_CONSUMER_SECRET
}

dependencies {
    final DEXMAKER_VERSION = '1.2'
    final SUPPORT_LIB_VERSION = '23.2.1'
    final LEAK_CANARY_VERSION = '1.4-beta2'
    // TODO: rest of versions

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:design:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:support-v4:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:recyclerview-v7:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:cardview-v7:${SUPPORT_LIB_VERSION}"

    // retrofit
    compile 'com.squareup.retrofit2:retrofit:2.0.1'
    compile 'com.squareup.retrofit2:converter-gson:2.0.1'

    // glide
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.florent37:glidepalette:1.0.6'

    // dagger
    provided 'org.glassfish:javax.annotation:10.0-b28'

    def daggerCompiler = 'com.google.dagger:dagger-compiler:2.0'
    apt daggerCompiler
    androidTestApt daggerCompiler
    compile 'com.google.dagger:dagger:2.0'

    // stetho and realm
    compile 'com.facebook.stetho:stetho:1.3.1'
    compile 'com.uphyca:stetho_realm:0.9.0'

    // leakcanary
    debugCompile "com.squareup.leakcanary:leakcanary-android:${LEAK_CANARY_VERSION}"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${LEAK_CANARY_VERSION}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${LEAK_CANARY_VERSION}"

    // shared test dependencies
    def junit = 'junit:junit:4.12'
    def mockito = 'org.mockito:mockito-core:1.10.19'

    // unit test dependencies
    testCompile junit
    testCompile mockito
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.hamcrest:hamcrest-integration:1.3'

    // espresso test dependencies
    androidTestCompile junit
    androidTestCompile mockito
    def exclude_support_annotations = 'support-annotations'
    androidTestCompile('com.android.support.test:runner:0.3') {
        exclude module: exclude_support_annotations
    }
    androidTestCompile('com.android.support.test:rules:0.3') {
        exclude module: exclude_support_annotations
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.1') {
        exclude module: exclude_support_annotations
    }
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
        exclude module: exclude_support_annotations
    }

    androidTestCompile "com.google.dexmaker:dexmaker:${DEXMAKER_VERSION}"
    androidTestCompile "com.google.dexmaker:dexmaker-mockito:${DEXMAKER_VERSION}"
}
