apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.tomaszrykala.discogs"
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'com.tomaszrykala.discogs.MockDiscogsAppRunner'
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet true
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        ignoreWarnings false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

dependencies {
    final DEXMAKER_VERSION = '1.2'
    final SUPPORT_LIB_VERSION = '24.2.1'
    final LEAK_CANARY_VERSION = '1.4-beta2'
    final RETROFIT_VERSION = '2.1.0'
    final HAMCREST_VERSION = '1.3'
    final TEST_SUPPORT_VERSION = '0.3'
    final ESPRESSO_VERSION = '2.2.1'
    final RXJAVA_VERSION = '1.1.0'

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:design:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:support-v4:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:recyclerview-v7:${SUPPORT_LIB_VERSION}"
    compile "com.android.support:cardview-v7:${SUPPORT_LIB_VERSION}"

    // retrofit
    compile "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    compile "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"
    compile "com.squareup.retrofit2:adapter-rxjava:${RETROFIT_VERSION}"

    // rxjava
    compile "io.reactivex:rxjava:${RXJAVA_VERSION}"
    compile "io.reactivex:rxandroid:${RXJAVA_VERSION}"

    // glide
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.florent37:glidepalette:1.0.6'

    // dagger
    provided 'org.glassfish:javax.annotation:10.0-b28'

    def daggerCompiler = 'com.google.dagger:dagger-compiler:2.0'
    apt daggerCompiler
    androidTestApt daggerCompiler
    compile 'com.google.dagger:dagger:2.0'

    // stetho and realm
    compile 'com.facebook.stetho:stetho:1.3.1'
    compile 'com.uphyca:stetho_realm:0.9.0'

    // leakcanary
    debugCompile "com.squareup.leakcanary:leakcanary-android:${LEAK_CANARY_VERSION}"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${LEAK_CANARY_VERSION}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${LEAK_CANARY_VERSION}"

    // shared test dependencies
    def junit = 'junit:junit:4.12'
    def mockito = 'org.mockito:mockito-core:1.10.19'

    // unit test dependencies
    testCompile junit
    testCompile mockito
    testCompile "org.hamcrest:hamcrest-core:${HAMCREST_VERSION}"
    testCompile "org.hamcrest:hamcrest-library:${HAMCREST_VERSION}"
    testCompile "org.hamcrest:hamcrest-integration:${HAMCREST_VERSION}"

    // espresso test dependencies
    androidTestCompile junit
    androidTestCompile mockito
    def exclude_support_annotations = 'support-annotations'
    androidTestCompile("com.android.support.test:runner:${TEST_SUPPORT_VERSION}") {
        exclude module: exclude_support_annotations
    }
    androidTestCompile("com.android.support.test:rules:${TEST_SUPPORT_VERSION}") {
        exclude module: exclude_support_annotations
    }
    androidTestCompile("com.android.support.test.espresso:espresso-core:${ESPRESSO_VERSION}") {
        exclude module: exclude_support_annotations
    }
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:${ESPRESSO_VERSION}") {
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
        exclude module: exclude_support_annotations
    }

    androidTestCompile "com.google.dexmaker:dexmaker:${DEXMAKER_VERSION}"
    androidTestCompile "com.google.dexmaker:dexmaker-mockito:${DEXMAKER_VERSION}"
}
